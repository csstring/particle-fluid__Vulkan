#version 460

layout(set = 0, binding = 0, rgba32f) uniform image2D velocity;
layout(set = 0, binding = 1, rgba32f) uniform image2D pressure;

layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
void main() {
    ivec2 dtID = ivec2(gl_GlobalInvocationID.xy);
    int width = imageSize(velocity).x;
    int height = imageSize(velocity).y;

    float left = imageLoad(pressure, ivec2(dtID.x == 0 ? width - 1 : dtID.x - 1, dtID.y)).x;
    float right = imageLoad(pressure, ivec2(dtID.x == width - 1 ? 0 : dtID.x + 1, dtID.y)).x;
    float up = imageLoad(pressure, ivec2(dtID.x, dtID.y == height - 1 ? 0 : dtID.y + 1)).x;
    float down = imageLoad(pressure, ivec2(dtID.x, dtID.y == 0 ? height - 1 : dtID.y - 1)).x;
    
    vec4 dp = vec4(right - left, up - down, 0.0f, 0.0f) * 0.5f;

    imageStore(velocity, dtID, imageLoad(velocity, dtID) - dp);
}
